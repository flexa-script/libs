// JSON standard lib
// MIT License
// Copyright (c) 2024 Carlos Eduardo de Borba Machado

namespace flx;

using flx.std.collections.dictionary;

fun _json_stringify_value(value: any): string;
fun json_stringify(dict: Dictionary): string;

fun _json_stringify_value(value: any): string {
	var json: string = "";

	if (typeof(value) == typeof(Dictionary)) {
		json = json_stringify(value);
	} else if (is_struct(value)) {
		json = json_stringify(parse_struct(value));
	} else if (is_textual_type(value)) {
		json = '"' + string(value) + '"';
	} else if (is_array(value)) {
		json = "[";
		foreach (var v in value) {
			json += _json_stringify_value(v) + ",";
		}

		if (json[len(json) - 1] == ',') {
			json = substr(json, 0, len(json) - 1);
		}

		json += "]";

	} else {
		json += string(value);
	}
	
	return json;
}

fun json_stringify(dict: Dictionary): string {
	var json: string = "{";
	var visited_list = create_list();
	var current_stack = create_stack();
	var current = dict.root;

	while (current != null) {
		if (not exists(visited_list, current.key)) {
			push(current_stack, current);
			append(visited_list, current.key);
			json += '"' + current.key + "\":" + _json_stringify_value(current.value) + ',';
		}
		if (current.left != null and not exists(visited_list, current.left.key)) {
			current = current.left;
		} else if (current.right != null and not exists(visited_list, current.right.key)) {
			current = current.right;
		} else {
			pop(current_stack);
			current = len(current_stack) > 0 ? peek(current_stack) : null;
		}
	}

	if (json[len(json) - 1] == ',') {
		json = substr(json, 0, len(json) - 1);
	}

	json += "}";

	return json;
}
